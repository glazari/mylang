fn main() -> u64 {
  init(); 
  print_nln(123);
  print_nln(1);
  print_nln(10);
  print_nln(0);
  print_nln(0000);
  print_nln(10000);
  print_hexln(10000);
}

// --------------------------------------------------
// prelude-like area, helper functions
// --------------------------------------------------

global current_brk = 0;
fn init_brk() -> u64 {
  current_brk = brk(0);
}

fn malloc(num_bytes: u64) -> u64 {
  let pointer: u64 = current_brk;
  let expected_new: u64 = pointer + num_bytes;
  current_brk = brk(expected_new);
  if (current_brk != expected_new) {
    // allocation failed, exit program
    exit(1);
  }
  return pointer;
}

fn init() -> u64 {
  init_brk();
  init_print_nln();
  init_num_to_string();
}


global print_nln_bff = 0;
fn init_print_nln() -> u64 {
  print_nln_bff = malloc(1024);
}
fn print_nln(number: u64) -> u64 {
  let len: u64 = num_to_string(number, print_nln_bff, 10);
  write(1, print_nln_bff, len);
}
fn print_hexln(number: u64) -> u64 {
  asm {
    mov rax, {print_nln_bff}
    mov dx, 0x7830 ; 0x
    mov [rax], dx
  }
  let len: u64 = num_to_string(number, print_nln_bff + 2, 16);
  write(1, print_nln_bff, len + 2);
}


global num_to_string_bff = 0;
fn init_num_to_string() -> u64 {
  num_to_string_bff = malloc(50);
}
fn num_to_string(number: u64, address: u64, base: u64) -> u64 {
  let digit: u64 = 0;
  let tmp_add: u64 = num_to_string_bff;

  do {
    digit = number % base;
    number = number / base;
    if (digit < 11) {
    	digit = digit + 48; //convert to ascii digit
    } else {
    	digit = digit - 10 + 97; // convert to hex letters
    }
    // store in 
    asm {
      mov rdx, {digit}
      mov rax, {tmp_add}
      mov byte [rax], dl 
    }
    tmp_add = tmp_add + 1;
    
  } while (number > 0);

  let len: u64 = tmp_add - num_to_string_bff;
  tmp_add = tmp_add - 1;

  let i: u64 = 0;
  while (i < len) {
    asm {
      mov rax, {tmp_add}
      mov byte dl, [rax]
      mov rax, {address} ; address points to a location that has the address
      mov byte [rax], dl
    }
    i = i + 1;
    address = address + 1;
    tmp_add = tmp_add - 1;
  }

  asm {
    mov rax, {address}
    mov byte [rax], 10 ; add new line
  }
  len = len + 1;
  return len;
}

fn ptr_get(addr: u64) -> u64 {
  let out: u64 = 0;
  asm {
    mov rax, {addr}
    mov rbx, [rax]
    mov {out}, rbx
  }
  return out;
}

fn ptr_store(addr: u64, val: u64) -> u64 {
  asm {
    mov rax, {val}
    mov rbx, {addr} 
    mov [rbx], rax
  }
}

// --------------------------------------------------
//  System calls
// --------------------------------------------------

fn exit(code: u64) -> u64 { // exits program with exit_code
  asm {
    mov rax, 60     
    mov rdi, {code}
    syscall
  }
}

fn brk(address: u64) -> u64 {
  let new_brk: u64 = 0;
  asm {
    mov rax, 12 ; brk syscall
    mov rdi, {address}
    syscall
    mov {new_brk}, rax ; store new program break
  }
  return new_brk;
}

fn write(fd: u64, msg_addr: u64, len: u64) -> u64 {
  asm {
    mov rax, 1
    mov rdi, {fd}
    mov rsi, {msg_addr}
    mov rdx, {len}
    syscall
  }
}
